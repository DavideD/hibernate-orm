import org.hibernate.build.HibernateBuildPlugin

/*
 * Hibernate, Relational Persistence for Idiomatic Java
 *
 * License: GNU Lesser General Public License (LGPL), version 2.1 or later.
 * See the lgpl.txt file in the root directory or <http://www.gnu.org/licenses/lgpl-2.1.html>.
 */

description = 'Integration tests for chekcing bytecode enhancments'

apply from: rootProject.file( 'gradle/java-module.gradle' )

// Override -source and -target
ext.baselineJavaVersion = 11
sourceCompatibility = project.baselineJavaVersion
targetCompatibility = project.baselineJavaVersion

// Checkstyle fails for module-info
checkstyleMain.exclude '**/module-info.java'

buildscript {
	repositories {
		jcenter()
		mavenCentral()
	}
}

dependencies {
	compile project( ':hibernate-core' )
	provided project( ':hibernate-testing' )

	provided libraries.jpa
	provided libraries.jboss_ejb_spec_jar
	provided libraries.jboss_annotation_spec_jar

	testCompile("org.assertj:assertj-core:3.11.1")
	testCompile project( ':hibernate-testing' )
//	testCompile libraries.assertJ
	testCompile libraries.shrinkwrap_api
	testCompile libraries.shrinkwrap
	testCompile libraries.shrinkwrap_descriptors_api_javaee
	testCompile libraries.shrinkwrap_descriptors_impl_javaee
	testCompile libraries.wildfly_arquillian_container_managed
	testCompile libraries.arquillian_junit_container
	testCompile libraries.arquillian_protocol_servlet
}

processTestResources {
	doLast {
		copy {
			from file( 'src/test/resources' )
			into file( "${buildDir}/resources/test" )
			include 'arquillian.xml'
			expand wildFlyInstallDir: project( ':hibernate-orm-modules' ).wildFlyInstallDir,
				   hibernateMajorMinorVersion: "${project.ormVersion.family}",
				   arquillianDeploymentExportDir: "${rootProject.buildDir.absolutePath}/arquillian-deployments"
		}
	}
}

test.dependsOn ':hibernate-orm-modules:prepareWildFlyForTests'

test {
	useJUnit()
}

